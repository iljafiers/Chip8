; ---- minimal chip8 game ---
OPTION BINARY
ALIGN OFF  

; V0-2 	scrap
; V3 	player x
; V4 	player y
; V5	enemy x
; V6	enemy y
; VF    collision detection

; keys:
; #2 = up
; #4 = left
; #5 = space
; #6 = right
; #8 = down
; #A = Ctrl
; #B = shift
; #C = Z
; #F = Space


;HIGH		; Uncomment this for Hires Superchip mode (128x64)

GAMEINIT:

CLS		; clear screen
CALL INITPLAYER ; init player @546
CALL INITENEMY  ; init enemy @592

CALL DRAWPLAYER ; draw player on startpos @552
CALL DRAWENEMY  ; draw enemy on startpos @598


; ================== game loop ================
GAMELOOP:	
	
  CALL DRAWPLAYER   ; draw xor player (clear last pos)
  CALL HANDLEINPUT  ; move player @558	
  CALL DRAWPLAYER   ; draw player on new pos @552

  CALL DRAWENEMY
  CALL MOVEENEMY	;@604
  CALL DRAWENEMY	;@598

  ; check collision, init player upon collision
  SNE VF,1
  JP GAMEINIT

  LD V0,1
  LD DT,V0
  CALL WAITDELAY	;@618
  

JP GAMELOOP

; ================== functions ================
INITPLAYER:				; PC=546
  LD V3,32
  LD V4,25
  RET

; drawplayer  (uses: v3,v4,I)
DRAWPLAYER:			; PC=552
  LD I,SPR_DUDE	   ;Set I to SPR_DUDE adress (626)
  DRW V3,V4,6
  RET

; handle input (uses v0)
HANDLEINPUT:		;PC=558
  ; check down
  LD V0,#8	;Set V0 to Key
  SKP V0	;Check Key (skip next instruction if pressed)
  JP checkup    ;Skip
  ADD V4,1	;y++

  checkup:	; PC=566
  LD V0,#2	;Set V0 to Key
  SKP V0	;Check Key
  JP checkleft	;skip to next check
  ADD V4,#FF	;y--

  checkleft:	;PC=574
  LD V0,#4	;Set V0 to Key
  SKP V0	;Check Key
  JP checkright	;skip to next check
  ADD V3,#FF	;x--

  checkright:	;PC=582
  LD V0,#6	;Set V0 to Key
  SKP V0	;Check Key
  JP checkdone	;skip to next check
  ADD V3,#1	;x++

  checkdone:	;PC=590
  RET

INITENEMY:		; @592
  LD V5,0
  RND V6,15
  RET

DRAWENEMY:				; PC-598
  LD I,SPR_ENEMY		;Set I to SPR_ENEMY adress (632)
  DRW V5,V6,5
  RET

MOVEENEMY:				; PC=604
  ADD V5,1				; enemy x+=1

  ; compare V1>60
  LD V0,V5	
  LD V1,60
  SUB V0,V1
  SNE VF,1				; PC=612
  CALL INITENEMY
  RET

WAITDELAY:		;PC=618
  LD V0,DT	   ;Set V0 to DelayTimer
  SE V0,0	   ;Check zero
  JP WAITDELAY     
RET  

; ================== graphics ================
SPR_DUDE:
 DB $..1111..
 DB $...11...
 DB $11111111
 DB $...11...
 DB $..1..1..
 DB $111..111

SPR_ENEMY:
 DB $.111111.
 DB $11111111
 DB $1..11..1
 DB $111..111
 DB $..1111..
